; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; 06/01/17  dce  added uninstall sections to tidy up left behind data
;                if we ship a new version of wpkg with this, then we'll need to update [Run] & [UninstallRun] sections
; 05/01/18  dce  new cwRsync version 5.5.0
;                in [Run] section use skipifsilent so will be ignored if run silent (auto)
; 08/01/18  dce  fix takeown and icacls syntax in wpkgsync.bat
;                uninstall section should ask if we're to uninstall wpkg and remove all the data
; 11/01/18  dce  we need waituntilterminated on installs regardless of what the documentation implies
; 21/05/18  dce  new version of wpkgsync.bat
; 17/09/18  dce  new version of wpkgsync.bat adds /fast option
; 22/10/18  dce  .4 new version of wpkgsync.bat adds rsync --times
; 26/02/20  dce  .6 use any relevant packages\exclude file 
; 28/02/20  dce  .10 use /setup & separate option for full synch
; 07/05/20  dce  .11 use the new way of getting last user and boot time
; 15/05/20  dce  .12 use LastBootUpTime now, remove no longer required script dependancy
; 03/07/20  dce  .15 autogenerate exclude file, abort if not admin
; 09/11/20  dce  .16 get systeminfo and serial number
; 23/11/20  dce  .20 use "logfile" for systeminfo and serial number
; 23/11/20  dce  6.2.0.20 use 64 bit CWrsync 6.2.0
; 31/12/20  dce  .21 add wpkgcontrol
; 07/03/21  dce  .22 fold wpkgcontrol into wpkgsync
; 11/03/21  dce  .22 build version depending on Architecture
; 01/12/21  dce  remove wpkgcontrol

[Setup]
; ============================================================
; use cwRsync version for base version number, change it here, uncomment ArchitecturesAllowed for x64
ArchitecturesAllowed=x64
#define ScriptVersion "22"
;#if "x64" == ArchitecturesAllowed 
	#define RsyncVer "6.2.1"       
;#else
;	#define RsyncVer "5.5.0"    
;#endif

#define MyCompany "company"
; ============================================================
; #define MyAppVersion {#RsyncVer} + "." + {#ScriptVersion}
AppVersion={#RsyncVer}.{#ScriptVersion}
OutputBaseFilename=wpkgsync_setup.{#RsyncVer}.{#ScriptVersion}.{#MyCompany}
AppName=WpkgSync
#include AddBackslash(SourcePath) + "config." + AddBackslash(MyCompany) + "include.iss" 
UninstallDisplayName=WpkgSync
DefaultDirName={commonpf}\WpkgSync
DisableDirPage=yes
DefaultGroupName=WpkgSync
DisableProgramGroupPage=yes 
OutputDir=C:\progs\wpkgsync
InfoBeforeFile=C:\progs\wpkgsync\config.{#MyCompany}\readme.txt
UninstallDisplayIcon={app}\wpkgsync.ico
SetupIconFile=wpkgsync.ico
Compression=lzma
SolidCompression=yes
; ExtraDiskSpaceRequired for the wpkg folder download (bytes) we need about 2 Gb = 2,147,483,648 bytes 
ExtraDiskSpaceRequired=2147483648

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[InstallDelete]
; delete files before the installer runs, we want to remove the .x86 .dll files
Type: files; Name: "{app}\*.dll";

[Files]
Source: "C:\progs\wpkgsync\wpkgsync.bat";                             DestDir: "{app}";                                                    Flags: ignoreversion
Source: "C:\progs\wpkgsync\wpkgsync.ico";                             DestDir: "{app}";                                                    Flags: ignoreversion
Source: "C:\progs\wpkgsync\config.{#MyCompany}\wpkgsync.ini";         DestDir: "{app}";                                                    Flags: ignoreversion
Source: "C:\progs\wpkgsync\config.{#MyCompany}\.ssh\wpkgsyncuser.id"; DestDir: "{commonappdata}\wpkgsync\.ssh";                            Flags: ignoreversion
Source: "C:\progs\wpkgsync\config.{#MyCompany}\.ssh\known_hosts";     DestDir: "{commonappdata}\wpkgsync\.ssh"; DestName: "known_hosts";   Flags: ignoreversion
Source: "C:\progs\wpkgsync\cwRsync.{#RsyncVer}\bin\*";                DestDir: "{app}";                                                    Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

; push the wpkg client so we can run the install
Source: "C:\progs\wpkgsync\client\WPKG Client 1.3.14-x32.msi";      DestDir: "{commonappdata}\wpkg\client";   Check: not IsWin64;        Flags: ignoreversion
Source: "C:\progs\wpkgsync\client\WPKG Client 1.3.14-x64.msi";      DestDir: "{commonappdata}\wpkg\client";   Check: IsWin64;            Flags: ignoreversion
Source: "C:\progs\wpkgsync\client\wpkg_local_settings.xml";         DestDir: "{commonappdata}\wpkg\client";                              Flags: ignoreversion

; to silently install wpkg, you use this
; msiexec /qn /i WPKGSetup.msi SETTINGSFILE=f:\wpkg\images\setup\settings.xml
; to silently update the settings of an already installed WPKG Client, use:
; %PROGRAMFILES%\WPKG\wpkginst.exe --SETTINGSFILE=f:\wpkg\images\setup\settings.xml

[Run]
Filename: "{commonappdata}\wpkg\client\WPKG Client 1.3.14-x32.msi"; Parameters: "/qn SETTINGSFILE={commonappdata}\wpkg\client\wpkg_local_settings.xml"; StatusMsg: "Installing the x32 WPKG client..."; Description: "Install the WPKG client (if it's not installed)"; Check: not IsWin64; Flags: postinstall skipifsilent shellexec runascurrentuser waituntilterminated
Filename: "{commonappdata}\wpkg\client\WPKG Client 1.3.14-x64.msi"; Parameters: "/qn SETTINGSFILE={commonappdata}\wpkg\client\wpkg_local_settings.xml"; StatusMsg: "Installing the x64 WPKG client..."; Description: "Install the WPKG client (if it's not installed)"; Check: IsWin64; Flags: postinstall skipifsilent shellexec runascurrentuser waituntilterminated
; to update the settings of an already installed WPKG Client, use:
Filename: "{commonpf32}\WPKG\wpkginst.exe"; Parameters: "--SETTINGSFILE={commonappdata}\wpkg\client\wpkg_local_settings.xml"; StatusMsg: "Updating the settings of the WPKG client..."; Description: "Update the settings of the WPKG client (if it's already installed)"; Check: not IsWin64; Flags: postinstall skipifsilent runascurrentuser waituntilterminated
Filename: "{commonpf64}\WPKG\wpkginst.exe"; Parameters: "--SETTINGSFILE={commonappdata}\wpkg\client\wpkg_local_settings.xml"; StatusMsg: "Updating the settings of the WPKG client..."; Description: "Update the settings of the WPKG client (if it's already installed)"; Check: IsWin64; Flags: postinstall skipifsilent runascurrentuser waituntilterminated
; minimal synchronisation
Filename: "{app}\wpkgsync.bat"; StatusMsg: "Synchronising the data..."; Description: "Run the initial synchronisation"; Parameters: "/setup"; Flags: postinstall skipifsilent shellexec runascurrentuser waituntilterminated
; and then run the first synchronisation
Filename: "{app}\wpkgsync.bat"; StatusMsg: "Synchronising the data..."; Description: "Run the first full synchronisation (should run in the background for about 1 hour)"; Flags: unchecked postinstall skipifsilent shellexec runascurrentuser waituntilterminated runminimized hidewizard

[UninstallDelete]
; include here actions to run after the uninstaller runs, so we want to remove the \ProgramData\wpkgsync data folder
Type: filesandordirs; Name: "{commonappdata}\wpkgsync";



